buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = "https://repo.spongepowered.org/maven" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
        classpath "com.modrinth.minotaur:Minotaur:2.+"
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: "org.spongepowered.mixin"
apply plugin: 'maven-publish'
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "com.modrinth.minotaur"

def buildNumber = System.getenv("GPR_BUILD_NUMBER") ? "+" + System.getenv("GPR_BUILD_NUMBER") : ""
version = mod_version + buildNumber

archivesBaseName = "${mod_id}-forge-${minecraft_version}"
group = mod_group

minecraft {
    mappings channel: 'official', version: minecraft_version

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName "Client"

            arg "-mixin.config=${mod_id}.mixins.json"

            property "mixin.env.disableRefMap", "true"
            property "mixin.debug.export", "true"
            property "mixin.debug.export.decompile", "false"
            property "mixin.debug.verbose", "true"
            property "mixin.debug.dumpTargetOnFailure", "true"
            property "mixin.checks", "true"
            property "mixin.hotSwap", "true"

            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                    source project(":shared").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName "Server"

            arg "-mixin.config=${mod_id}.mixins.json"

            property "mixin.env.disableRefMap", "true"
            property "mixin.debug.export", "true"
            property "mixin.debug.export.decompile", "false"
            property "mixin.debug.verbose", "true"
            property "mixin.debug.dumpTargetOnFailure", "true"
            property "mixin.checks", "true"
            property "mixin.hotSwap", "true"

            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                    source project(":shared").sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName "Data"

            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=${mod_id}.mixins.json"

            property "mixin.env.disableRefMap", "true"
            property "mixin.debug.export", "true"
            property "mixin.debug.export.decompile", "false"
            property "mixin.debug.verbose", "true"
            property "mixin.debug.dumpTargetOnFailure", "true"
            property "mixin.checks", "true"
            property "mixin.hotSwap", "true"

            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                "${mod_id}" {
                    source sourceSets.main
                    source project(":shared").sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs 'src/generated/resources'
        }
        ext.refMap = "${mod_id}.refmap.json"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
    compileOnly project(":shared")

    implementation fg.deobf("net.blay09.mods:balm:${balm_forge_version}")

    compileOnly fg.deobf("me.shedaniel.cloth:cloth-config-forge:5.0.38")
    testCompileOnly fg.deobf("me.shedaniel.cloth:cloth-config-forge:5.0.38")

    compileOnly fg.deobf("mezz.jei:jei-1.18.2:9.7.0.193:api")
//    runtimeOnly fg.deobf("mezz.jei:jei-1.18.2:9.7.0.193")

    compileOnly fg.deobf("curse.maven:the-one-probe-245211:3606616")

    compileOnly fg.deobf("curse.maven:badpackets-615134:3773642")
    compileOnly fg.deobf("curse.maven:wthit-455982:3856256")
    compileOnly fg.deobf("curse.maven:jade-324717:3834997")

    compileOnly fg.deobf(group: 'info.journeymap', name: 'journeymap-api', version: project.journeymap_api_forge_version, changing: true)
//    runtimeOnly fg.deobf("curse.maven:journeymap-${project.journeymap_project_id}:${project.journeymap_forge_file_id}")

    implementation "org.jetbrains:annotations:22.0.0"
}

tasks.withType(JavaCompile) {
    source(project(":shared").sourceSets.main.allSource)
}

processResources {
    from project(":shared").sourceSets.main.resources
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = mod_id + "-forge"

            artifact jar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/" + github_repo)
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
            }
        }
    }
}

curseforge {
    apiKey = project.findProperty("curseforge.api_key") ?: System.getenv("CF_API_KEY") ?: "none"
    project {
        id = curseforge_forge_project_id
        releaseType = curseforge_release_type
        changelogType = 'markdown'
        changelog = file('../changelog.md')
        addGameVersion minecraft_version
        addGameVersion 'Forge'

        relations {
            requiredLibrary 'balm'
        }
    }
}

modrinth {
    token = project.findProperty("modrinth.token") ?: System.getenv("MODRINTH_TOKEN") ?: "none"
    projectId = project.modrinth_forge_project_id
    versionType = project.modrinth_release_type
    versionNumber = project.version + "+forge-" + project.minecraft_version
    uploadFile = jar
    changelog = rootProject.file("changelog.md").text
    gameVersions = [project.minecraft_version]
    syncBodyFrom = rootProject.file("modrinth.md").text
    dependencies {
        required.project "balm"
    }
}