buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"

def BUILD_NUMBER = System.getenv("BUILD_NUMBER") ?: patch_version

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

version = major_version + "." + minor_version + "." + BUILD_NUMBER
group = "net.blay09.mods"
archivesBaseName = mod_name + "_" + minecraft_version

sourceSets {
    main {
        ext.refMap = "refmap.waystones.json"
    }
}


minecraft {
    mappings channel: mappings_channel, version: mappings_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
//            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=mixins.waystones.json"

            property "mixin.env.disableRefMap", "true"
            property "mixin.debug.export", "true"
            property "mixin.debug.export.decompile", "false"
            property "mixin.debug.verbose", "true"
            property "mixin.debug.dumpTargetOnFailure", "true"
            property "mixin.checks", "true"
            property "mixin.hotSwap", "true"

            mods {
                waystones {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
//            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=mixins.waystones.json"

            property "mixin.env.disableRefMap", "true"
            property "mixin.debug.export", "true"
            property "mixin.debug.export.decompile", "false"
            property "mixin.debug.verbose", "true"
            property "mixin.debug.dumpTargetOnFailure", "true"
            property "mixin.checks", "true"
            property "mixin.hotSwap", "true"

            mods {
                waystones {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=mixins.waystones.json"

            property "mixin.env.disableRefMap", "true"
            property "mixin.debug.export", "true"
            property "mixin.debug.export.decompile", "false"
            property "mixin.debug.verbose", "true"
            property "mixin.debug.dumpTargetOnFailure", "true"
            property "mixin.checks", "true"
            property "mixin.hotSwap", "true"

            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/')

            mods {
                waystones {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    flatDir {
        dirs "run/obfmods"
    }
}

dependencies {
    minecraft([
            group  : "${forge_group}",
            name   : "forge",
            version: "${minecraft_version}-${forge_version}"
    ])

    compileOnly fg.deobf("mezz.jei:jei-1.16.3:7.6.0.49:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.16.3:7.6.0.49")

    compile fg.deobf("curse.maven:hwyla-253449:3033593")
    compile fg.deobf("curse.maven:the-one-probe-245211:3108607")

    compile fg.deobf("curse.maven:journeymap-32274:3355768")

    compile fg.deobf("curse.maven:abnormals-core-382216:3233486")
    compile fg.deobf("curse.maven:endergetic-291509:3154938")

    // annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'

    def obfModsDir = new File("run/obfmods")
    if(obfModsDir.exists()) {
        obfModsDir.eachFile { file ->
            def parts = file.name.split('-', 2)
            def dep = "ignored:" + parts[0] + ":" + parts[1].replace(".jar", "");
            runtimeOnly fg.deobf(dep)
        }
    }
}

jar {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : "BlayTheNinth",
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "BlayTheNinth",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    "MixinConfigs": "mixins.waystones.json"])
    }
}
