subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    java.withSourcesJar()
    java.withJavadocJar()

    tasks.withType(GenerateModuleMetadata).tap {
        configureEach {
            enabled = false // Do not publish dependencies in pom
        }
    }

    configurations {
        testImplementation.extendsFrom compileOnly
    }

    jar {
        manifest {
            attributes([
                    "Specification-Title"     : mod_id,
                    "Specification-Vendor"    : "BlayTheNinth",
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : "BlayTheNinth",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
            if (forge_mixins.toBoolean()) {
                attributes([
                        "MixinConfigs": mod_id + ".mixins.json"
                ])
            }
        }
    }

    repositories {
        mavenCentral()

        maven {
            url "https://www.cursemaven.com"

            content {
                includeGroup 'curse.maven'
            }
        }

        maven { url "https://nexus.twelveiterations.com/repository/maven-public/" }

        // For Cloth Config and REI
        maven { url "https://maven.shedaniel.me/" }

        // For JEI
        maven { url "https://dvs1.progwml6.com/files/maven/" }

        // For WTHIT
        maven { url "https://maven.bai.lol" }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = java_version as int
    }

    tasks.withType(Javadoc).tap {
        configureEach {
            failOnError false
        }
    }

    if (balm_version.endsWith("SNAPSHOT")) {
        configurations.configureEach {
            resolutionStrategy.cacheChangingModulesFor 10, 'minutes'
        }
    }
}