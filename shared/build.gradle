plugins {
    id('java')
    id('org.spongepowered.gradle.vanilla') version '0.2.1-SNAPSHOT'
    id 'maven-publish'
}

group = mod_group
version = mod_version
archivesBaseName = "${mod_id}-common-${minecraft_version}"

minecraft {
    version(minecraft_version)
}

dependencies {
    compileOnly "org.spongepowered:mixin:$mixin_version"

    compileOnly("net.blay09.mods:balm-common:$balm_version") {
        exclude(group: "net.blay09.mods", module: "shared-bridge")
        changing = balm_version.endsWith("SNAPSHOT")
    }

    compileOnly("me.shedaniel.cloth:cloth-config-fabric:$cloth_config_version") {
        transitive = false
    }

    compileOnly "mezz.jei:jei-$jei_minecraft_version-common-api:$jei_version"

    compileOnly "mcp.mobius.waila:wthit-api:mojmap-$wthit_version"
}

apply from: 'dependencies.gradle'

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_id}" }
    }
}

sourcesJar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_id}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = mod_id + "-common"
            version = project.version + (!project.version.endsWith("SNAPSHOT") ? "+" + minecraft_version : "")

            artifact jar

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact project.javadocJar {
                classifier 'javadoc'
            }
        }
    }

    repositories {
        maven {
            var releasesRepoUrl = "https://nexus.twelveiterations.com/repository/maven-releases/"
            var snapshotsRepoUrl = "https://nexus.twelveiterations.com/repository/maven-snapshots/"
            url = uri(version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl)
            name = "twelveIterationsNexus"
            credentials(PasswordCredentials)
        }
    }
}