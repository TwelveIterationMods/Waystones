plugins {
    id('java')
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
}
group = mod_group
version = mod_version
archivesBaseName = "${mod_id}-common-${minecraft_version}"

repositories {
    maven {
        url = 'https://maven.minecraftforge.net'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    compileOnly 'org.spongepowered:mixin:0.8.4'

    modCompileOnly("me.shedaniel.cloth:cloth-config-fabric:5.0.34") {
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "net.fabricmc")
    }

    compileOnly "mcp.mobius.waila:wthit-api:mojmap-5.5.1"

    modCompileOnly("net.blay09.mods:balm-fabric:${balm_version}") {
        transitive = false
        changing = balm_version.endsWith("SNAPSHOT")
    }
}

apply from: 'dependencies.gradle'

configurations {
    testImplementation.extendsFrom compileOnly
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_id}" }
    }
}

loom {
    mixin {
        useLegacyMixinAp = false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = mod_id + "-common"
            version = project.version + (!project.version.endsWith("SNAPSHOT") ? "+" + minecraft_version : "")

            from components.java
        }
    }

    repositories {
        maven {
            var releasesRepoUrl = "https://nexus.twelveiterations.com/repository/maven-releases/"
            var snapshotsRepoUrl = "https://nexus.twelveiterations.com/repository/maven-snapshots/"
            url = uri(version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl)
            name = "twelveIterationsNexus"
            credentials(PasswordCredentials)
        }
    }
}