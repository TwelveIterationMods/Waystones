plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
	id 'idea'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id "com.modrinth.minotaur" version "2.+"
}

group = mod_group
archivesBaseName = "${mod_id}-fabric-${minecraft_version}"
version = mod_version

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()

	modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
	implementation project(":shared")

	modImplementation("net.blay09.mods:balm-fabric:${balm_version}") {
		transitive = false
		changing = balm_version.endsWith("SNAPSHOT")
	}

	implementation "org.jetbrains:annotations:22.0.0"


	modImplementation "curse.maven:modmenu-308702:$modmenu_version"

	modImplementation("me.shedaniel.cloth:cloth-config-fabric:$cloth_config_version")

	modImplementation "mezz.jei:jei-$jei_minecraft_version-fabric-api:$jei_version"
	if (jei_minecraft_version == minecraft_version) {
		modRuntimeOnly "mezz.jei:jei-$jei_minecraft_version-fabric:$jei_version"
	}

	modCompileOnly("me.shedaniel:RoughlyEnoughItems-api:$rei_version") {
		transitive = false
	}

	modImplementation "curse.maven:jade-324717:$jade_forge_version"
	modCompileOnly "mcp.mobius.waila:wthit-api:mojmap-$wthit_version"
}

apply from: 'dependencies.gradle'

loom {
	runs {
		client {
			client()
			setConfigName("fabric Client")
			ideConfigGenerated(true)
			runDir("run")
		}
		server {
			server()
			setConfigName("fabric Server")
			ideConfigGenerated(true)
			runDir("run")
		}
		data {
			inherit client
			setConfigName("fabric Data")
			ideConfigGenerated(true)
			runDir("build/datagen")

			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${project(":shared").file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=${mod_id}"
		}
	}
}

sourceSets {
	main {
		resources {
			srcDirs 'src/generated/resources'
		}
	}
}

processResources {
	from project(":shared").sourceSets.main.resources
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
	source(project(":shared").sourceSets.main.allSource)
}

jar {
	from(rootProject.file("LICENSE")) {
		rename { "${it}_${mod_id}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = mod_id + "-fabric"
			version = project.version + (!project.version.endsWith("SNAPSHOT") ? "+" + minecraft_version : "")

			from components.java
		}
	}

	repositories {
		maven {
			var releasesRepoUrl = "https://nexus.twelveiterations.com/repository/maven-releases/"
			var snapshotsRepoUrl = "https://nexus.twelveiterations.com/repository/maven-snapshots/"
			url = uri(version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl)
			name = "twelveIterationsNexus"
			credentials(PasswordCredentials)
		}
	}
}

curseforge {
	apiKey = project.findProperty("curseforge.api_key") ?: System.getenv("CURSEFORGE_TOKEN") ?: "none"
	project {
		id = curseforge_fabric_project_id
		releaseType = curseforge_release_type
		changelogType = 'markdown'
		changelog = rootProject.file('CHANGELOG.md')
		project.minecraft_versions.split(',').each {
			addGameVersion minecraft_version
		}
		addGameVersion 'Fabric'

		relations {
			requiredLibrary 'balm-fabric'
		}

		mainArtifact(remapJar)

		afterEvaluate {
			uploadTask.dependsOn(remapJar)
		}
	}
	options {
		forgeGradleIntegration = false
	}
}

modrinth {
	token = project.findProperty("modrinth.token") ?: System.getenv("MODRINTH_TOKEN") ?: "none"
	projectId = project.modrinth_fabric_project_id
	versionType = project.modrinth_release_type
	versionNumber = project.version + "+fabric-" + project.minecraft_version
	uploadFile = remapJar
	changelog = rootProject.file("CHANGELOG.md").text
	gameVersions = project.minecraft_versions.split(',').toList()
	syncBodyFrom = rootProject.file("modrinth.md").text
	loaders = ['fabric']
	dependencies {
		required.project "fabric-api"
		required.project "balm"
	}
}