plugins {
	id 'fabric-loom' version '0.8-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
def buildNumber = System.getenv("GPR_BUILD_NUMBER") ?: "0"
version = project.mod_version + "+" + buildNumber
group = project.maven_group

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	maven {
		url "https://maven.pkg.github.com/ModdingForBlockheads/Balm"
		credentials {
			username project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME")
			password project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
		}
	}

	// For Cloth Config
	maven { url "https://maven.shedaniel.me/" }

	// For ModMenu
	// maven { url "https://maven.terraformersmc.com/releases/" }

	// For CurseForge
	maven { url "https://www.cursemaven.com" }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	// mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	mappings minecraft.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
	modImplementation "net.blay09.mods:balm-fabric:1.1.0+135"

	modApi("me.shedaniel.cloth:cloth-config-fabric:5.0.34") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// modImplementation "com.terraformersmc:modmenu:2.0.2"
	modImplementation "curse.maven:modmenu-308702:3348711"

	// Roughly Enough Items
	modImplementation "curse.maven:roughly-enough-items-310111:3371046"
	modImplementation "curse.maven:architectury-api-419697:3372837"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.17 (21w19a) upwards uses Java 16.
	it.options.release = 16
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

sourceSets {
	shared {
		java {
			srcDirs '../shared/src/java/'
		}
		compileClasspath += sourceSets.main.compileClasspath
	}
	main {
		compileClasspath += sourceSets.shared.output
		runtimeClasspath += sourceSets.shared.output
	}
}

jar {
	from sourceSets.shared.output.classesDirs
	from sourceSets.shared.output.resourcesDir

	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}

	from("LICENSE-icons") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = project.archivesBaseName

			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/" + project.github_publish)
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
			}
		}
	}
}
